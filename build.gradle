plugins {
    id 'java-library'
    id "org.jetbrains.kotlin.jvm" version "1.7.21"
    id 'org.jetbrains.kotlin.plugin.lombok' version '1.7.21'

    id "io.freefair.lombok" version "6.4.3"
    id 'maven-publish'
}
allprojects {
    apply plugin: "java-library"
    apply plugin: "kotlin"
    apply plugin: "io.freefair.lombok"
    apply plugin: "org.jetbrains.kotlin.plugin.lombok"
    apply plugin: 'maven-publish'

    version = "1.0.5"

    compileJava {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
    }

    repositories {
        mavenCentral()
        mavenLocal()
    }

    java {
        withSourcesJar()
    }

    test {
        useJUnitPlatform()
    }

    dependencies {
        compileOnly("org.jetbrains:annotations:23.0.0")


        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
        testImplementation "org.mockito:mockito-core:3.+"
    }

    def moduleName = ""
    def lastProject = project

    while (lastProject != null) {
        def con = moduleName.isEmpty() ? "" : "-"
        moduleName = lastProject.name + con + moduleName
        lastProject = lastProject.parent
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from components.java

                artifactId = moduleName
                groupId = "ru.spliterash"
            }
        }
        repositories {
/*                    maven {
                        name = "folder"
                        url = "file://" + rootProject.projectDir + "/repo"
                    }*/
            maven {
                name = "nexus"
                url = "https://repo.spliterash.ru/spcore/"


                credentials {
                    username = findProperty('SPLITERASH_NEXUS_USR')
                    password = findProperty('SPLITERASH_NEXUS_PSW')
                }
            }
        }
    }
}

// Главный модуль имеет в себе все суб модули
subprojects.each { sub ->
    dependencies {
        api sub
    }
}